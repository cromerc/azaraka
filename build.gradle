/*
 * Copyright 2019 Chris Cromer
 *
 * Redistribution and use in source and binary forms, with or without modification, are permitted provided that the following conditions are met:
 *
 * 1. Redistributions of source code must retain the above copyright notice, this list of conditions and the following disclaimer.
 *
 * 2. Redistributions in binary form must reproduce the above copyright notice, this list of conditions and the following disclaimer in the documentation and/or other materials provided with the distribution.
 *
 * 3. Neither the name of the copyright holder nor the names of its contributors may be used to endorse or promote products derived from this software without specific prior written permission.
 *
 * THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
 *
 */

plugins {
	id 'idea'
	id 'java'
	id 'application'
	id 'distribution'
}

group 'cl.cromer.azaraka'
version '1.0.0'

sourceCompatibility = 1.8
targetCompatibility = 1.8

applicationName = 'Azaraka'
mainClassName = 'cl.cromer.azaraka.Azaraka'

idea {
	module {
		downloadJavadoc = true
		downloadSources = true
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation group: 'com.google.code.gson', name: 'gson', version: '2.+'
	//testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.+'
}

ext.sharedManifest = manifest {
	attributes 'Main-Class': "$mainClassName",
			'Class-Path': configurations.default.files.collect { "$it.name" }.join(' '),
			'Implementation-Title': 'Gradle',
			'Implementation-Version': "$gradle.gradleVersion"
}

jar {
	manifest = project.manifest {
		from sharedManifest
	}

	if (project.uberJar == "true") {
		from sourceSets.main.output

		dependsOn configurations.runtimeClasspath
		from {
			configurations.runtimeClasspath.findAll { it.name.endsWith('jar') }.collect { zipTree(it) }
		}
	}
	else {
		dependsOn tasks.withType(Copy)
	}
}

task copyDependencies(type: Copy) {
	from configurations.default
	into "$buildDir/libs"
}

plugins.withType(DistributionPlugin) {
	distTar {
		archiveExtension = 'tar.gz'
		compression = Compression.GZIP
	}
}

wrapper {
	gradleVersion = '5.6.2'
}